/obj/machinery/computer/bs_emitter
	name = "прикольная консоль телепортера"
	desc = "Необходима для телепортации всяких штук."
	icon_screen = "teleport"
	icon_keyboard = "teleport_key"
	//circuit = /obj/item/circuitboard/computer/turbine_computer
	ui_x = 500
	ui_y = 500
	var/obj/machinery/power/bs_emitter/BSE

/obj/machinery/computer/bs_emitter/Initialize()
	. = ..()
	return INITIALIZE_HINT_LATELOAD

/obj/machinery/computer/bs_emitter/LateInitialize()
	locate_machinery()

/obj/machinery/computer/bs_emitter/locate_machinery()
		BSE = locate(/obj/machinery/power/bs_emitter) in range(7, src)

/obj/machinery/computer/bs_emitter/ui_interact(mob/user, ui_key = "main", datum/tgui/ui = null, force_open = FALSE, \
									datum/tgui/master_ui = null, datum/ui_state/state = GLOB.default_state)
	ui = SStgui.try_update_ui(user, src, ui_key, ui, force_open)
	if(!ui)
		ui = new(user, src, ui_key, "BSEmitter", name, ui_x, ui_y, master_ui, state)
		ui.open()

/obj/machinery/computer/bs_emitter/ui_data(mob/user)
	var/list/data = list()

	data["connected"] = BSE ? TRUE : FALSE

	if(BSE)
		data["active"] = BSE.active
		data["expanding"] = BSE.expanding
		data["activeturfs"] = BSE.active_tiles.len ? TRUE : FALSE
		data["powernet"] = BSE.powernet ? TRUE : FALSE
		data["surplusKW"] = BSE.surplus()/1000
		data["loadKW"] = BSE.cur_load/1000

		data["radius"] = BSE.max_range
		data["t_x"] = BSE.target_x
		data["t_y"] = BSE.target_y
		data["t_z"] = BSE.target_z

	return data

/obj/machinery/computer/bs_emitter/ui_act(action, params)
	if(..())
		return

	switch(action)
		if("toggle")
			if(BSE)
				if(BSE.active)
					BSE.turn_off()
				else
					BSE.turn_on()
				. = TRUE

		if("reconnect")
			locate_machinery()
			. = TRUE

		if("setCoords")
			var/tx = text2num(params["newx"])
			var/ty = text2num(params["newy"])
			var/tz = text2num(params["newz"])
			if(BSE)
				BSE.set_coords(tx, ty, tz)
				. = TRUE

		if("setRadius")
			var/radius = text2num(params["radius"])
			if(BSE)
				BSE.max_range = radius
				. = TRUE


