/obj/item/firing_pin
	name = "электронный ударник"
	desc = "Небольшое устройство аутентификации, которое должно быть вставлено в приемник огнестрельного оружия, чтобы позволить сделать выстрел. Правила безопасности NT требуют, чтобы все новые конструкции включали это."
	icon = 'icons/obj/device.dmi'
	icon_state = "firing_pin"
	inhand_icon_state = "pen"
	worn_icon_state = "pen"
	flags_1 = CONDUCT_1
	w_class = WEIGHT_CLASS_TINY
	attack_verb_continuous = list("тычет")
	attack_verb_simple = list("тычет")
	var/fail_message = span_warning("НЕПРАВИЛЬНЫЙ ПОЛЬЗОВАТЕЛЬ.")
	var/selfdestruct = FALSE // Explode when user check is failed.
	var/force_replace = FALSE // Can forcefully replace other pins.
	var/pin_removeable = FALSE // Can be replaced by any pin.
	var/obj/item/gun/gun

/obj/item/firing_pin/New(newloc)
	..()
	if(istype(newloc, /obj/item/gun))
		gun = newloc

/obj/item/firing_pin/afterattack(atom/target, mob/user, proximity_flag)
	. = ..()
	if(proximity_flag)
		if(istype(target, /obj/item/gun))
			var/obj/item/gun/G = target
			var/obj/item/firing_pin/old_pin = G.pin
			if(old_pin && (force_replace || old_pin.pin_removeable))
				to_chat(user, span_notice("Убираю [old_pin] из [G]."))
				if(Adjacent(user))
					user.put_in_hands(old_pin)
				else
					old_pin.forceMove(G.drop_location())
				old_pin.gun_remove(user)

			if(!G.pin)
				if(!user.temporarilyRemoveItemFromInventory(src))
					return
				gun_insert(user, G)
				to_chat(user, span_notice("Вставляю [src] в [G]."))
			else
				to_chat(user, span_notice("Это оружие уже имеет ударник."))

/obj/item/firing_pin/emag_act(mob/user)
	if(obj_flags & EMAGGED)
		return
	obj_flags |= EMAGGED
	to_chat(user, span_notice("Перезаписываю механизм авторизации."))

/obj/item/firing_pin/proc/gun_insert(mob/living/user, obj/item/gun/G)
	gun = G
	forceMove(gun)
	gun.pin = src
	return

/obj/item/firing_pin/proc/gun_remove(mob/living/user)
	gun.pin = null
	gun = null
	return

/obj/item/firing_pin/proc/pin_auth(mob/living/user)
	return TRUE

/obj/item/firing_pin/proc/auth_fail(mob/living/user)
	if(user)
		user.show_message(fail_message, MSG_VISUAL)
	if(selfdestruct)
		if(user)
			user.show_message("<span class='danger'>Запуск механизма самоуничтожения...</span><br>", MSG_VISUAL)
			to_chat(user, span_userdanger("[gun] взорвался!"))
		explosion(get_turf(gun), -1, 0, 2, 3)
		if(gun)
			qdel(gun)


/obj/item/firing_pin/magic
	name = "магический осколок кристалла"
	desc = "Маленький вставленый осколок позволяет магичексому оружию стрелять."


// Test pin, works only near firing range.
/obj/item/firing_pin/test_range
	name = "ударник для тестовой площадки"
	desc = "Данный ударник позволяет протестировать оружие на тестовой площадке. В ином месте это не будет работать."
	fail_message = span_warning("НЕ НА ТЕСТОВОЙ ПЛОЩАДКЕ.")
	pin_removeable = TRUE

/obj/item/firing_pin/test_range/pin_auth(mob/living/user)
	if(!istype(user))
		return FALSE
	if (istype(get_area(user), /area/security/range))
		return TRUE
	return FALSE


// Implant pin, checks for implant
/obj/item/firing_pin/implant
	name = "иплантозависимый ударник"
	desc = "Этот ударник позволяет только авторизованным пользователям делать выстрел, причем пользователь должен быть имплантирован определенным устройством."
	fail_message = span_warning("НЕТ ИМПЛАНТА. В ДОСТУПЕ ОТКАЗАНО.")
	var/obj/item/implant/req_implant = null

/obj/item/firing_pin/implant/pin_auth(mob/living/user)
	if(user)
		for(var/obj/item/implant/I in user.implants)
			if(req_implant && I.type == req_implant)
				return TRUE
	return FALSE

/obj/item/firing_pin/implant/mindshield
	name = "ударник под «щит разума»"
	desc = "Этот защитный ударник позволяет использовать оружие только тем, кто имплантировал себе «щит разума»."
	icon_state = "firing_pin_loyalty"
	req_implant = /obj/item/implant/mindshield

/obj/item/firing_pin/implant/pindicate
	name = "синдикатский ударник"
	icon_state = "firing_pin_pindi"
	req_implant = /obj/item/implant/weapons_auth



// Honk pin, clown's joke item.
// Can replace other pins. Replace a pin in cap's laser for extra fun!
/obj/item/firing_pin/clown
	name = "веселый ударник"
	desc = "Усовершенствованый клованский ударник. Рекомендуется сунуть в капитанскую лазерную пушку для большей ржаки."
	color = "#FFFF00"
	fail_message = span_warning("ВОТ ЭТО ПРИКОЛ!")
	force_replace = TRUE

/obj/item/firing_pin/clown/pin_auth(mob/living/user)
	playsound(src, 'sound/items/bikehorn.ogg', 50, TRUE)
	return FALSE

// Ultra-honk pin, clown's deadly joke item.
// A gun with ultra-honk pin is useful for clown and useless for everyone else.
/obj/item/firing_pin/clown/ultra
	name = "ultra hilarious firing pin"

/obj/item/firing_pin/clown/ultra/pin_auth(mob/living/user)
	playsound(src.loc, 'sound/items/bikehorn.ogg', 50, TRUE)
	if(QDELETED(user))  //how the hell...?
		stack_trace("/obj/item/firing_pin/clown/ultra/pin_auth called with a [isnull(user) ? "null" : "invalid"] user.")
		return TRUE
	if(HAS_TRAIT(user, TRAIT_CLUMSY)) //clumsy
		return TRUE
	if(user.mind)
		if(user.mind.assigned_role == "Clown") //traitor clowns can use this, even though they're technically not clumsy
			return TRUE
		if(user.mind.has_antag_datum(/datum/antagonist/nukeop/clownop)) //clown ops aren't clumsy by default and technically don't have an assigned role of "Clown", but come on, they're basically clowns
			return TRUE
		if(user.mind.has_antag_datum(/datum/antagonist/nukeop/leader/clownop)) //Wanna hear a funny joke?
			return TRUE //The clown op leader antag datum isn't a subtype of the normal clown op antag datum.
	return FALSE

/obj/item/firing_pin/clown/ultra/gun_insert(mob/living/user, obj/item/gun/G)
	..()
	G.clumsy_check = FALSE

/obj/item/firing_pin/clown/ultra/gun_remove(mob/living/user)
	gun.clumsy_check = initial(gun.clumsy_check)
	..()

// Now two times deadlier!
/obj/item/firing_pin/clown/ultra/selfdestruct
	desc = "Усовершенствованый клованский ударник. Содержит малый заряд бананиума."
	selfdestruct = TRUE


// DNA-keyed pin.
// When you want to keep your toys for yourself.
/obj/item/firing_pin/dna
	name = "генный ударник"
	desc = "Связывает вас и оружие на генном уровне. Никто, кроме вас, не сможет выстрелить."
	icon_state = "firing_pin_dna"
	fail_message = span_warning("ХРОМОСОМЫ НЕ СОВПАДАЮТ.")
	var/unique_enzymes = null

/obj/item/firing_pin/dna/afterattack(atom/target, mob/user, proximity_flag)
	. = ..()
	if(proximity_flag && iscarbon(target))
		var/mob/living/carbon/M = target
		if(M.dna && M.dna.unique_enzymes)
			unique_enzymes = M.dna.unique_enzymes
			to_chat(user, span_notice("ДНК установлено."))

/obj/item/firing_pin/dna/pin_auth(mob/living/carbon/user)
	if(user && user.dna && user.dna.unique_enzymes)
		if(user.dna.unique_enzymes == unique_enzymes)
			return TRUE
	return FALSE

/obj/item/firing_pin/dna/auth_fail(mob/living/carbon/user)
	if(!unique_enzymes)
		if(user && user.dna && user.dna.unique_enzymes)
			unique_enzymes = user.dna.unique_enzymes
			to_chat(user, span_notice("ДНК установлено."))
	else
		..()

/obj/item/firing_pin/dna/dredd
	desc = "Связывает вас и оружие на генном уровне. Никто, кроме вас, не сможет выстрелить. А если попытаются, то взорвутся."
	selfdestruct = TRUE

// Paywall pin, brought to you by ARMA 3 DLC.
// Checks if the user has a valid bank account on an ID and if so attempts to extract a one-time payment to authorize use of the gun. Otherwise fails to shoot.
/obj/item/firing_pin/paywall
	name = "платный ударник"
	desc = "Ударник со встроенным купюроприемником."
	color = "#FFD700"
	fail_message = ""
	var/list/gun_owners = list() //list of people who've accepted the license prompt. If this is the multi-payment pin, then this means they accepted the waiver that each shot will cost them money
	var/payment_amount //how much gets paid out to license yourself to the gun
	var/obj/item/card/id/pin_owner
	var/multi_payment = FALSE //if true, user has to pay everytime they fire the gun
	var/owned = FALSE
	var/active_prompt = FALSE //purchase prompt to prevent spamming it

/obj/item/firing_pin/paywall/attack_self(mob/user)
	multi_payment = !multi_payment
	to_chat(user, span_notice("Установил ударник на [( multi_payment ) ? "обрабатывать платеж за каждый выстрел" : "единоразовый платеж по лицензии"]."))

/obj/item/firing_pin/paywall/examine(mob/user)
	. = ..()
	if(pin_owner)
		. += "<hr><span class='notice'>Данный ударник в данный момент авторизован для приема платежей на счет [pin_owner.registered_name].</span>"

/obj/item/firing_pin/paywall/gun_insert(mob/living/user, obj/item/gun/G)
	if(!pin_owner)
		to_chat(user, span_warning("ERROR: Проведите картой по ударнику, прежде чем вставлять в оружие!"))
		return
	gun = G
	forceMove(gun)
	gun.pin = src
	if(multi_payment)
		gun.desc += span_notice(" Этот [gun.name] имеет стоимость за выстрел [payment_amount] cr.[( payment_amount > 1 ) ? "s" : ""].")
		return
	gun.desc += span_notice(" Этот [gun.name] премиальный доступ за [payment_amount] cr.[( payment_amount > 1 ) ? "s" : ""].")
	return


/obj/item/firing_pin/paywall/gun_remove(mob/living/user)
	gun.desc = initial(desc)
	..()

/obj/item/firing_pin/paywall/attackby(obj/item/M, mob/user, params)
	if(istype(M, /obj/item/card/id))
		var/obj/item/card/id/id = M
		if(!id.registered_account)
			to_chat(user, span_warning("ERROR: У карты отсутствует банковский счет!"))
			return
		if(id != pin_owner && owned)
			to_chat(user, span_warning("ERROR: Ударник уже авторизован!"))
			return
		if(id == pin_owner)
			to_chat(user, span_notice("Отвязываю ударник от карты."))
			gun_owners -= user
			pin_owner = null
			owned = FALSE
			return
		var/transaction_amount = input(user, "Введите действительную сумму депозита для покупки оружия", "Денежный депозит") as null|num
		if(transaction_amount < 1)
			to_chat(user, span_warning("ERROR: Указана неверная сумма."))
			return
		if(!transaction_amount)
			return
		pin_owner = id
		owned = TRUE
		payment_amount = transaction_amount
		gun_owners += user
		to_chat(user, span_notice("Связываю карту с ударником."))

/obj/item/firing_pin/paywall/pin_auth(mob/living/user)
	if(!istype(user))//nice try commie
		return FALSE
	var/datum/bank_account/credit_card_details = user.get_bank_account()
	if(user in gun_owners)
		if(multi_payment && credit_card_details)
			if(credit_card_details.adjust_money(-payment_amount))
				pin_owner.registered_account.adjust_money(payment_amount)
				return TRUE
			to_chat(user, span_warning("ERROR: Недостаточно баланса пользователя для успешной транзакции!"))
			return FALSE
		return TRUE
	if(credit_card_details && !active_prompt)
		var/license_request = "Yes" //тгшники идут домой со своим спящим говном //alert(user, "Желаете заплатить [payment_amount] cr.[( payment_amount > 1 ) ? "s" : ""] за [( multi_payment ) ? "каждый выстрел [gun.name]" : "лицензия на пользование [gun.name]"]?", "Покупка оружия", "Да", "Нет")
		active_prompt = TRUE
		if(!user.canUseTopic(src, BE_CLOSE))
			active_prompt = FALSE
			return FALSE
		switch(license_request)
			if("Yes")
				if(credit_card_details.adjust_money(-payment_amount))
					pin_owner.registered_account.adjust_money(payment_amount)
					gun_owners += user
					to_chat(user, span_notice("Куплена лицензия!"))
					active_prompt = FALSE
					return FALSE //we return false here so you don't click initially to fire, get the prompt, accept the prompt, and THEN the gun
				to_chat(user, span_warning("ERROR: Недостаточно баланса пользователя для успешной транзакции!"))
				return FALSE
			if("No")
				to_chat(user, span_warning("ERROR: Пользователь отказался от лицензии на покупку оружия!"))
				return FALSE
	to_chat(user, span_warning("ERROR: У пользователя нет действующего банковского счета!"))
	return FALSE

// Explorer Firing Pin- Prevents use on station Z-Level, so it's justifiable to give Explorers guns that don't suck.
/obj/item/firing_pin/explorer
	name = "малонаселенный ударник"
	desc = "Ударник, используемый австралийскими силами, переоборудован, чтобы предотвратить сброс оружия на станцию"
	icon_state = "firing_pin_explorer"
	fail_message = span_warning("НЕ СТРЕЛЯЕТ НА СТАНЦИИ, ДРУЖОК!")

// This checks that the user isn't on the station Z-level.
/obj/item/firing_pin/explorer/pin_auth(mob/living/user)
	var/turf/station_check = get_turf(user)
	if(!station_check||is_station_level(station_check.z))
		to_chat(user, span_warning("Не могу использовать оружие на станции!"))
		return FALSE
	return TRUE

// Laser tag pins
/obj/item/firing_pin/tag
	name = "ударник для лазертага"
	desc = "Работает когда одет костюм для лазертага."
	fail_message = span_warning("КОСТЮМ ОТСУТСТВУЕТ.")
	var/obj/item/clothing/suit/suit_requirement = null
	var/tagcolor = ""

/obj/item/firing_pin/tag/pin_auth(mob/living/user)
	if(ishuman(user))
		var/mob/living/carbon/human/M = user
		if(istype(M.wear_suit, suit_requirement))
			return TRUE
	to_chat(user, span_warning("Нужно надеть [tagcolor] броню для лазертага!"))
	return FALSE

/obj/item/firing_pin/tag/red
	name = "красный ударник лазертага"
	icon_state = "firing_pin_red"
	suit_requirement = /obj/item/clothing/suit/redtag
	tagcolor = "red"

/obj/item/firing_pin/tag/blue
	name = "синий ударник лазертага"
	icon_state = "firing_pin_blue"
	suit_requirement = /obj/item/clothing/suit/bluetag
	tagcolor = "blue"

/obj/item/firing_pin/Destroy()
	if(gun)
		gun.pin = null
	return ..()
