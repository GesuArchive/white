/*
`````````````````````.~YGBPYYY5G#PY5555555YY55555555555555555555555555555555555555555555PPP55555555PB&@@@@@@@@@@@@@@@@@@
```````````````````.~?J5P5YYYYYY555YYYY555YY5555555555555555555555555555555555555555555555555555555555B@@@@@@@@@@@@@@@#G
``````````````````^7?JYYYY555YYYYYYYYYYY555555555555555555555555555555555555555555555555555555555555555P#&@@@@@@@@@&BP55
```````````````.^?JJJYYYYYYYYYYYYYYY555Y5555555555555555555555555555555555555555555555555555555555555555PB&@@@@@@@#5Y555
`````````````.~?JJYYYYYYYYYYYYYYYYYYY5PP5YYYYYYYYYYYY5555555555555555555555555555555555555555555555555P55G##@@@@&G555555
```````````.~?JYYYYYYYYYYYYYYYYYYYYYY55PG5Y55YYYYYYYY55YY5555P5555555555555555555555555555555555555555GG55GBB@@@P55555PG
`````````.~?JJYYYYYYYYYYYYYYYYYYYY55555YP#B5555YYYY55Y5PGGGGBBBBBGGGPP555555555555555555555555555555555GGP5P&@@&555GBGGP
`````````^?JJJYYYYYYYYYYYYYYYYYYYYY555555YG&G555555Y5PB#BG555555555PPGGBBBGGPP55555555555555555555555555PPGPB@&&@PP##GPPP
......`~JJJYYYYYYYYYYYYYYYYYYYYYYYY55Y5555GG5555Y5GB#G5555555555555555555PPGBBBGGP55555555555555555555555PP&@&&@&&G55555
......!JJYYYYYYYYYYYYYYYYYYYYYYYYYY55YY55555555PB#B5YY5555555555555555555555555PPGBBGGP5555555555555555555G@&&&@@G555555
...`.7JJYYYYYYYYYYYYYYYYYYYYY55YY55YYYY55555YP##G5Y555555555555555555555555555555555PGBBG55555555555555555G@&&&&@#555555
```!JJYYYYYYYYYYYYYYYYYYYYY5B#BP5YY555555555#G55GB#&&#G55Y55555555555555555555555555555#B555555555PGP55555&@&&&@@#55555
```^JJYYYYYYYYYYYYYYYYYYYY5B#&&@&&BPYYY55555PGG#&@@@@@&&&#GP55555555555555555555555555555##555555555BB55555G&@&&@@@G5555
.``!JYYYYYYYYYYYYYYYYYYYY5#&&###&&&&#P555555GG#&&@&@&&&&&&@&&BP5Y5555555555555555555555555#B55555555B&G555555G#&@@@&5555
.``7YYYYYYYYYYYYYYYYYYY55G&&&#####&&&&BPP555GG&&&&&&@@&&&&&&&&&&#G555555555555555555555555P&G5555555P#B555555555G#&@B555
.`.?5YYYYYYYYYYYY55YYYYGBB&&@@@&&&&#&&&#GPY5PG&&&&&&&&&&&&&&#######G5555555555555555555555P##55555555B#P555555555PB&#555
...?P5YYYYYYYYYYYYYYYY5GG&&#&&&&&&&&&&&&#BG55B&#&@@@&##BPYJJ??????YBB555555555555555555555P#&B5555555PBG555555555G#&&555
```?G#YYYYYYYYYYYYYY5YPG&#BB##&&&&&&@&&&&##5YB&&#@&G5J????????JJ????P#GP555555555555555555G#&#55555555PB555555555G&&&555
```^5&GYYYYYYYYYYYYYY5G&G?????J5GB##&&#5G&#PYG#YJGJ??????????????????5&B555555555555555555G###55555555PBP555555555#&&555
..``!P&PYYYYYYYYYYYYY5#B????????????J5PJ?YPYJYJ???????????????????????G#G555555555555555555B#P555555555BP55555555PB#&P55
..`..7B#5YYYYYYYYYYYYP#J???????????????????????????????????????????JJ?JB#G55555555555555555B#5555555555BP55555555PB#&P55
....~:?GBPYYYYYYYYY55#5??????????????????????????????????????????????J?5#&B555555555555555P&P5555555555BG55555555PG#&P55
....7!:7GGPYYYYYYYYYG#J????????????????????????????????????????????????JB#B555555555555555##P5555555555BB55555555PP#@P55
...`^J?^!YB#5Y5YYYY5B5??????????????????????????????????????????????????5#P5B&B#555555555BB555555555555BB5555555555&@P55
``.`:?JJ~!YGB5YYYYYPB???????????????????????????????????????????????????JBGBGP##G5555555B&&P55555555555BG555555555P@@P55
555J.7JJJ?7Y55YY55YBP???????????????????????????????????????????JJJY5YJ??5&G??B#BG55555#@#&P55555555555GP555555555G@@G55
@@@@~:JJJJJJY5YY555&P???????????????????????????????????????JYPGBBBBG5JJ?JY?J?Y&BGP55G##&##55555555555PG5555555555G@@B55
@@@@P.7JJJJJ5BB5YY5&5?????????????????????????????????????JP#BGGP5J???JJJ??JJJ?B#PPG#BY5&B#55555555555PG5555555555B@@B55
@@@@@~.?JJJJB&@&GYP#J????????????????????????????????????JB#PYJ???JJJJJJJJJJJJ?5&GBB5J?P&BB55555555555GP5555555555#@@#55
@@@@@#..?JJYP#&BG###???????JJYYY55YJJ????????????????????55J???JJJJJJJJJJJ???J5B&BPJJJ?P&GG55555555555BP555555555P&@@&55
@@@@@@B:.!JJ5G#&JY#B?????JY5PGGGGGBB#BPYJ??????????????????????J????JJJ???J5B&&&BP5YJJ?P#PP5555555555PBPP55555555G@@@&55
&@@@@@@&7.^JY5B&B??J??????????????JJJYY5J??????????????????????JJY555YJ5G&@@@#Y??JJJJJJBGGP5555555555GGGP55555555B@@@&P5
G&@@@@@@@#7:!J5G&BJ??????????????????????????????????????????J55PP5PB#&@@&BP5YJJJJJJJ?Y#GB55555555555BPGP55555555#@@@@P5
P&@@@@@@@@@#?~!J5##Y????????????????????????????????????????JJY5PB##BGPYJ?????JJJJJJJ?G&BG5555555555PBP&B55555555&@@@@P5
P&@@@@@@@@@@@#!^7YB#P?????????????JJJJJJJJ???????????????????JJYYJJ??????JJJJJJJJJJJJJ#&G55555555555GBG@B5555555P&@@@@P5
P&@@@@@@@@@@@@@G~7PG##5JGBGP5YYJJY5PPPPPPP5YJ???????????????J???????????JJJJJJJJJJJJ?5&B555555555555BG##55555555G@@@@@P5
P&@@@@@@@@@@@@@@G.7&#&&&B###&@@@@&&&&&#BGP5YJ?????????????JJJJJ??????JJJJJJJJJJJJJJJ?B#555555555555PBB&P55555555B@@@@@P5
P&@@@@@@@@@@@@@@P :#BP&@&57?YG55YYYJJJ???????????????????JJJJJJJJJJJJJJJJJJJJJJJJJJJ5&G555555555555GB#B555555555G@@@@@G5
P&@@@@@@@@@@@@@@Y 7GBYB##@BY??????????????????????????JJJJJJJJJJJJJJJJJJJJJJJJJJJJJJ&#5555555555555BBG5555555555P@@@@@B5
P&@@@@@@@@@@@@@@7^B5P5BGYP#&GJ????????????????????????JJJJJJJJJJJJJJJJJJJJJJJJJJJJ?G&5555555555555P#G5555555555PP@@@@@G5
P&@@@@@@@@@@@@@&^?@55PPP55YPB#P????????????????????JJJJJJJJJJJJJJJJJJJJJJJJJJJJJJ?5&P5555555555555B#P555555555PBG@@@@@G5
P&@@@@@@@@@@@@@B.P@GYPPP5555YP##5????????????????JJJJJJJJJJJJJJ??JJJJJJJJJJJJJJJ?Y&B5555555555555P#G5555555555G#G@@@@@B5
P&@@@@@@@@@@@@@J.&@#JPG55555555G&#Y?????JJ???????JJJJJJJJJJJJJJYPP5JJJJJJJJJJJ??Y&#55555555555555GB55555555555B&G@@@@@#P
P&@@@@@@@@@@@@&:!@@&YPB55555555YP@@GJ??????JJJJJJJJJJJJJJJY5PPP55JJJJJJJJJJJ?JP#@&P55555555555555#G5P555555555#&G@@@@@#G
P&@@@@@@@@@@@@#.J@@@PP#555555555P#@@&GY?????JJJJJJJJJJJJJJYYJJJJJJJJJJJJJJJ5G#&&&GGP555555555555P#P5P555555555B&B@@@@@##
P&@@@@@@@@@@@@B.G@@@B5B555555555GB##&#&#BG5YJ??JJJJJJJJJJJJJJJJJJJJJJJJJJJB#GPB&GPP5555555PPPPP5B&GP55555555PPP##@@@@&&#
P&@@@@@@@@@&&@5:&@@@#5B555555555GB###55PG#&&&#G5YJJJJ????JJJJJJJ??JYPG5JJG&P5P##PG55555555PPPPPP#&G555555555PPGBB&@@@#&G
P&@@@@@@@@@@@@J~@@@@&5P555555555GG#BB555555PP#&&&&##BGGP55555YYPG#&#GYJJJ&@BBP#GBP5555555PPPP55G&&G5PPP555555PGGG&@@@#B5
P&@@@@@@@@@@@@?J@@@@&55555555555G5BGBP5555555GB#&#5PPGGB#&#&&@@@&B5JJY5PPG5Y#&BG#P55PPPPPPPPPGG#&&GPPPP5555555GP5#@@&BP5
P&@@@@@@@@@@@&!G@@@@&5555555555555BGG555555555B##P555555G#GB##&GJY5PGGP5JJJJP#G#G5PP55555555PBG&&BPPPP55555555PP5G@@#P5P
P&@@@@@@@@@@@P~&@@@@B5555555555555BGP55555555B&GP555PP5P#B#B#&#GBBBPYYJJJJJYBG##PPPPP55555P5BBB&#GPPPPPPPPPPPPPP5G@&PPPP
P&@@@@@@@@@@@!5@@@@@B5555555555555BG55555555B&P5555555PBBBB&@@@#P5JJJJJJJY5BBB&P5PPPPPPPPPPP#G&&BBPPPPPPPPPPPPPP5G@B555P
P&@@@@@@@@@@&7&@@@@@B5555555555555BP555555P##P5PPGBB#&@&&&&&&&&@GJJJJJY5PB&&B&GPPPPPPPPPPPPPBB#G#GPPPPPPPPPPPPPP5##55P#@
P&@@@@@@@@@@G?@@@@@@P5555555555555BP55555P#BGGB##&&&#@@@&&&&&&&&@#PPGGGGB#&B#GPPPPPPPPPPPPPPG&&BBPPPPPPPPPPPPPPPB#PG&@@@
G&@@@@@@@@@@YP@@@@@&5555555555555PBP555PB##GGPG#&&##&@@@@@@@@@&&B#&#&&@@@@BBG5PPPPPPPPPPPPP5#&G##PPPPPPPPPPPPPPB&&&@@@@@
G&@@@@@@@@@&J&@@@@@GPP55555555555GB55G##BGB#&&&&&&&&&&&@@@&####B#&@@@@@@@BGPPPPPPPPPPPPPPPPP&#PGB&GPPPPPPPPPPPG&#GP&@@@@
G&@@@@@@@@@#5@@@@@&PBP555555555PGGBP&@&&@@@@@@&&&&&&&&@&#BGB&@@@@@@@@@@@#GPPPPPPPPPPPPPPPPP##&GPPG#PPPPPPPPPPPPP5PPB@@@@
#&@@@@@@@@@GB@@@@@BP#G555555555#&#&@@@@@@@@&&&&&&&&&&#&&&@@@@@@@@@@@@@@&PPPPPPPPPPPPPPPPPPP&PP#YG5&BPPPPPPPPPPPPPPGP&@@@
*/

#define CALL_BOT_COOLDOWN 900

//Not sure why this is necessary...
/proc/AutoUpdateAI(obj/subject)
	var/is_in_use = 0
	if (subject!=null)
		for(var/A in GLOB.ai_list)
			var/mob/living/silicon/ai/M = A
			if ((M.client && M.machine == subject))
				is_in_use = 1
				subject.attack_ai(M)
	return is_in_use


/mob/living/silicon/ai
	name = JOB_AI
	real_name = JOB_AI
	icon = 'icons/mob/ai.dmi'
	icon_state = "ai"
	move_resist = MOVE_FORCE_OVERPOWERING
	density = TRUE
	status_flags = CANSTUN|CANPUSH
	a_intent = INTENT_HARM //so we always get pushed instead of trying to swap
	sight = SEE_TURFS | SEE_MOBS | SEE_OBJS
	see_in_dark = 8
	hud_type = /datum/hud/ai
	med_hud = DATA_HUD_MEDICAL_BASIC
	sec_hud = DATA_HUD_SECURITY_BASIC
	d_hud = DATA_HUD_DIAGNOSTIC_ADVANCED
	mob_size = MOB_SIZE_LARGE
	radio = /obj/item/radio/headset/silicon/ai
	can_buckle_to = FALSE
	native_fov = null
	var/battery = 200 //emergency power if the AI's APC is off
	var/list/network = list("ss13")
	var/obj/machinery/camera/current
	var/list/connected_robots = list()
	var/aiRestorePowerRoutine = POWER_RESTORATION_OFF
	var/requires_power = POWER_REQ_ALL
	var/can_be_carded = TRUE
	var/icon/holo_icon //Default is assigned when AI is created.
	var/obj/controlled_equipment //A piece of equipment, to determine whether to relaymove or use the AI eye.
	var/radio_enabled = TRUE //Determins if a carded AI can speak with its built in radio or not.
	radiomod = ";" //AIs will, by default, state their laws on the internal radio.
	///Used as a fake multitoool in tcomms machinery
	var/obj/item/multitool/aiMulti
	var/mob/living/simple_animal/bot/Bot
	var/tracking = FALSE //this is 1 if the AI is currently tracking somebody, but the track has not yet been completed.
	var/datum/effect_system/spark_spread/spark_system //So they can initialize sparks whenever

	//MALFUNCTION
	var/datum/module_picker/malf_picker
	var/list/datum/ai_module/current_modules = list()
	var/can_dominate_mechs = FALSE
	var/shunted = FALSE	//1 if the AI is currently shunted. Used to differentiate between shunted and ghosted/braindead

	var/control_disabled = FALSE	// Set to 1 to stop AI from interacting via Click()
	var/malfhacking = FALSE		// More or less a copy of the above var, so that malf AIs can hack and still get new cyborgs -- NeoFite
	var/malf_cooldown = 0		//Cooldown var for malf modules, stores a worldtime + cooldown

	var/obj/machinery/power/apc/malfhack
	var/explosive = FALSE		//does the AI explode when it dies?

	var/mob/living/silicon/ai/parent
	var/camera_light_on = FALSE
	var/list/obj/machinery/camera/lit_cameras = list()

	var/datum/trackable/track = new

	var/last_paper_seen = null
	var/can_shunt = TRUE
	var/last_announcement = "" 		// For AI VOX, if enabled
	var/turf/waypoint //Holds the turf of the currently selected waypoint.
	var/waypoint_mode = FALSE		//Waypoint mode is for selecting a turf via clicking.
	var/call_bot_cooldown = 0		//time of next call bot command
	var/obj/machinery/power/apc/apc_override		//Ref of the AI's APC, used when the AI has no power in order to access their APC.
	var/nuking = FALSE
	var/obj/machinery/doomsday_device/doomsday_device

	var/mob/camera/ai_eye/eyeobj
	var/sprint = 10
	var/cooldown = 0
	var/acceleration = 1

	var/obj/structure/ai_core/deactivated/linked_core //For exosuit control
	var/mob/living/silicon/robot/deployed_shell = null //For shell control
	var/datum/action/innate/deploy_shell/deploy_action = new
	var/datum/action/innate/deploy_last_shell/redeploy_action = new
	var/datum/action/innate/choose_modules/modules_action
	var/chnotify = 0

	var/multicam_on = FALSE
	var/atom/movable/screen/movable/pic_in_pic/ai/master_multicam
	var/list/multicam_screens = list()
	var/list/all_eyes = list()
	var/max_multicams = 6
	var/display_icon_override

	var/list/cam_hotkeys = new/list(9)
	var/cam_prev

	var/datum/robot_control/robot_control
	/// Station alert datum for showing alerts UI
	var/datum/station_alert/alert_control
	///remember AI's last location
	var/atom/lastloc

	var/atom/movable/screen/ai/modpc/interfaceButton

/mob/living/silicon/ai/Initialize(mapload, datum/ai_laws/L, mob/target_ai)
	. = ..()
	if(!target_ai) //If there is no player/brain inside.
		new/obj/structure/ai_core/deactivated(loc) //New empty terminal.
		return INITIALIZE_HINT_QDEL //Delete AI.

	ADD_TRAIT(src, TRAIT_NO_TELEPORT, AI_ANCHOR_TRAIT)
	if(L && istype(L, /datum/ai_laws))
		laws = L
		laws.associate(src)
	else
		make_laws()

	if(target_ai.mind)
		target_ai.mind.transfer_to(src)
		if(mind.special_role)
			mind.store_memory("As an AI, you must obey your silicon laws above all else. Your objectives will consider you to be dead.")
			to_chat(src, span_userdanger("You have been installed as an AI! "))
			to_chat(src, span_danger("You must obey your silicon laws above all else. Your objectives will consider you to be dead."))

	to_chat(src, "<B>You are playing the station's AI. The AI cannot move, but can interact with many objects while viewing them (through cameras).</B>")
	to_chat(src, "<B>To look at other parts of the station, click on yourself to get a camera menu.</B>")
	to_chat(src, "<B>While observing through a camera, you can use most (networked) devices which you can see, such as computers, APCs, intercoms, doors, etc.</B>")
	to_chat(src, "To use something, simply click on it.")
	to_chat(src, "Use say :b to speak to your cyborgs through binary.")
	to_chat(src, "For department channels, use the following say commands:")
	to_chat(src, ":o - AI Private, :c - Command, :s - Security, :e - Engineering, :u - Supply, :v - Service, :m - Medical, :n - Science.")
	show_laws()
	to_chat(src, "<b>These laws may be changed by other players, or by you being the traitor.</b>")

	job = JOB_AI

	create_eye()
	if(client)
		INVOKE_ASYNC(src, PROC_REF(apply_pref_name),"ai",client)

	INVOKE_ASYNC(src, PROC_REF(set_core_display_icon))


	holo_icon = getHologramIcon(icon('icons/mob/ai.dmi',"default"))

	spark_system = new /datum/effect_system/spark_spread()
	spark_system.set_up(5, 0, src)
	spark_system.attach(src)

	add_verb(src, /mob/living/silicon/ai/proc/show_laws_verb)

	create_modularInterface()

	aiMulti = new(src)
	aicamera = new/obj/item/camera/siliconcam/ai_camera(src)

	deploy_action.Grant(src)

	if(isturf(loc))
		add_verb(src, list(/mob/living/silicon/ai/proc/ai_network_change, \
		/mob/living/silicon/ai/proc/ai_statuschange, /mob/living/silicon/ai/proc/ai_hologram_change, \
		/mob/living/silicon/ai/proc/botcall, /mob/living/silicon/ai/proc/control_integrated_radio, \
		/mob/living/silicon/ai/proc/set_automatic_say_channel))

	GLOB.ai_list += src
	GLOB.shuttle_caller_list += src

	builtInCamera = new (src)
	builtInCamera.network = list("ss13")

	ADD_TRAIT(src, TRAIT_PULL_BLOCKED, ROUNDSTART_TRAIT)
	ADD_TRAIT(src, TRAIT_HANDS_BLOCKED, ROUNDSTART_TRAIT)

	alert_control = new(src, list(ALARM_ATMOS, ALARM_FIRE, ALARM_POWER, ALARM_CAMERA, ALARM_BURGLAR, ALARM_MOTION), list(z))
	RegisterSignal(alert_control, COMSIG_ALARM_TRIGGERED, PROC_REF(alarm_triggered))
	RegisterSignal(alert_control, COMSIG_ALARM_CLEARED, PROC_REF(alarm_cleared))

/mob/living/silicon/ai/key_down(_key, client/user)
	if(findtext(_key, "numpad")) //if it's a numpad number, we can convert it to just the number
		_key = _key[7] //strings, lists, same thing really
	switch(_key)
		if("`", "0")
			if(cam_prev)
				eyeobj.setLoc(cam_prev)
			return
		if("1", "2", "3", "4", "5", "6", "7", "8", "9")
			_key = text2num(_key)
			if(user.keys_held["Ctrl"]) //do we assign a new hotkey?
				cam_hotkeys[_key] = eyeobj.loc
				to_chat(src, "Location saved to Camera Group [_key].")
				return
			if(cam_hotkeys[_key]) //if this is false, no hotkey for this slot exists.
				cam_prev = eyeobj.loc
				eyeobj.setLoc(cam_hotkeys[_key])
				return
	return ..()

/mob/living/silicon/ai/Destroy()
	GLOB.ai_list -= src
	GLOB.shuttle_caller_list -= src
	SSshuttle.autoEvac()
	QDEL_NULL(eyeobj) // No AI, no Eye
	QDEL_NULL(spark_system)
	QDEL_NULL(malf_picker)
	QDEL_NULL(doomsday_device)
	QDEL_NULL(robot_control)
	QDEL_NULL(aiMulti)
	QDEL_NULL(alert_control)
	malfhack = null
	current = null
	Bot = null
	controlled_equipment = null
	linked_core = null
	apc_override = null
	return ..()

/// Removes all malfunction-related abilities from the AI
/mob/living/silicon/ai/proc/remove_malf_abilities()
	QDEL_NULL(modules_action)
	for(var/datum/ai_module/AM in current_modules)
		for(var/datum/action/A in actions)
			if(istype(A, initial(AM.power_type)))
				qdel(A)

/mob/living/silicon/ai/ignite_mob()
	return FALSE

/mob/living/silicon/ai/proc/set_core_display_icon(input, client/C)
	if(client && !C)
		C = client
	if(!input && !C?.prefs?.preferred_ai_core_display)
		icon_state = initial(icon_state)
	else
		var/preferred_icon = input ? input : C.prefs.preferred_ai_core_display
		icon_state = resolve_ai_icon(preferred_icon)

/mob/living/silicon/ai/verb/pick_icon()
	set category = "AI Commands"
	set name = "Set AI Core Display"
	if(incapacitated())
		return
	icon = initial(icon)
	icon_state = "ai"
	cut_overlays()
	var/list/iconstates = GLOB.ai_core_display_screens
	for(var/option in iconstates)
		if(option == "Random")
			iconstates[option] = image(icon = src.icon, icon_state = "ai-random")
			continue
		if(option == "Portrait")
			iconstates[option] = image(icon = src.icon, icon_state = "ai-portrait")
			continue
		iconstates[option] = image(icon = src.icon, icon_state = resolve_ai_icon(option))

	view_core()
	var/ai_core_icon = show_radial_menu(src, src , iconstates, radius = 42)

	if(!ai_core_icon || incapacitated())
		return

	display_icon_override = ai_core_icon
	set_core_display_icon(ai_core_icon)

/mob/living/silicon/ai/get_status_tab_items()
	. = ..()
	if(stat != CONSCIOUS)
		. += "Systems nonfunctional"
		return
	. += "System integrity: [(health + 100) * 0.5]%"
	if(isturf(loc)) //only show if we're "in" a core
		. += "Backup Power: [battery * 0.5]%"
	. += "Connected cyborgs: [length(connected_robots)]"
	for(var/r in connected_robots)
		var/mob/living/silicon/robot/connected_robot = r
		var/robot_status = "Nominal"
		if(connected_robot.shell)
			robot_status = "AI SHELL"
		else if(connected_robot.stat != CONSCIOUS || !connected_robot.client)
			robot_status = "OFFLINE"
		else if(!connected_robot.cell || connected_robot.cell.charge <= 0)
			robot_status = "DEPOWERED"
		//Name, Health, Battery, Model, Area, and Status! Everything an AI wants to know about its borgies!
		. += "[connected_robot.name] | S.Integrity: [connected_robot.health]% | Cell: [connected_robot.cell ? "[connected_robot.cell.charge]/[connected_robot.cell.maxcharge]" : "Empty"] | \
		Model: [connected_robot.designation] | Loc: [get_area_name(connected_robot, TRUE)] | Status: [robot_status]"
	. += "AI shell beacons detected: [LAZYLEN(GLOB.available_ai_shells)]" //Count of total AI shells

/mob/living/silicon/ai/proc/ai_call_shuttle()
	if(control_disabled)
		to_chat(usr, span_warning("Wireless control is disabled!"))
		return

	var/can_evac_or_fail_reason = SSshuttle.canEvac(src, FALSE)
	if(can_evac_or_fail_reason != TRUE)
		to_chat(usr, span_alert("[can_evac_or_fail_reason]"))
		return

	var/reason = tgui_input_text(src, "Что послужило причиной вызова шаттла? (Минимум [CALL_SHUTTLE_REASON_LENGTH] символов.)", "Подтверждение Вызова Шаттла")

	if(incapacitated())
		return

	if(trim(reason))
		SSshuttle.requestEvac(src, reason)

	// hack to display shuttle timer
	if(!EMERGENCY_IDLE_OR_RECALLED)
		var/obj/machinery/computer/communications/C = locate() in GLOB.machines
		if(C)
			C.post_status("shuttle")

/mob/living/silicon/ai/can_interact_with(atom/A)
	. = ..()
	var/turf/ai = get_turf(src)
	var/turf/target = get_turf(A)
	if (.)
		return

	if(!target)
		return

	if ((ai.z != target.z) && !is_station_level(ai.z))
		return FALSE

	if (istype(loc, /obj/item/aicard))
		if (!ai || !target)
			return FALSE
		return ISINRANGE(target.x, ai.x - interaction_range, ai.x + interaction_range) && ISINRANGE(target.y, ai.y - interaction_range, ai.y + interaction_range)
	else
		return GLOB.cameranet.checkTurfVis(get_turf(A))

/mob/living/silicon/ai/cancel_camera()
	view_core()

/mob/living/silicon/ai/verb/toggle_anchor()
	set category = "AI Commands"
	set name = "Toggle Floor Bolts"
	if(!isturf(loc)) // if their location isn't a turf
		return // stop
	if(stat == DEAD)
		return
	if(incapacitated())
		if(battery < 50)
			to_chat(src, span_warning("Insufficient backup power!"))
			return
		battery = battery - 50
		to_chat(src, span_notice("You route power from your backup battery to move the bolts."))
	var/is_anchored = FALSE
	if(move_resist == MOVE_FORCE_OVERPOWERING)
		move_resist = MOVE_FORCE_NORMAL
		REMOVE_TRAIT(src, TRAIT_NO_TELEPORT, AI_ANCHOR_TRAIT)
	else
		is_anchored = TRUE
		move_resist = MOVE_FORCE_OVERPOWERING
		ADD_TRAIT(src, TRAIT_NO_TELEPORT, AI_ANCHOR_TRAIT)

	to_chat(src, "<b>You are now [is_anchored ? "" : "un"]anchored.</b>")
	// the message in the [] will change depending whether or not the AI is anchored

/mob/living/silicon/ai/proc/ai_mob_to_structure()
	disconnect_shell()
	ShutOffDoomsdayDevice()
	var/obj/structure/ai_core/deactivated/ai_core = new(get_turf(src), /* skip_mmi_creation = */ TRUE)
	if(!make_mmi_drop_and_transfer(ai_core.brain, the_core = ai_core))
		return FALSE
	qdel(src)
	return TRUE

/mob/living/silicon/ai/proc/make_mmi_drop_and_transfer(obj/item/mmi/the_mmi, the_core)
	var/mmi_type
	//if(posibrain_inside)
	//	mmi_type = new/obj/item/mmi/posibrain(src, /* autoping = */ FALSE)
	//else
	mmi_type = new/obj/item/mmi(src)
	if(hack_software)
		new/obj/item/malf_upgrade(get_turf(src))
	the_mmi = mmi_type
	the_mmi.brain = new /obj/item/organ/brain(the_mmi)
	the_mmi.brain.organ_flags |= ORGAN_FROZEN
	the_mmi.brain.name = "мозг [real_name]"
	the_mmi.name = "[initial(the_mmi.name)]: [real_name]"
	the_mmi.set_brainmob(new /mob/living/brain(the_mmi))
	the_mmi.brainmob.name = src.real_name
	the_mmi.brainmob.real_name = src.real_name
	the_mmi.brainmob.container = the_mmi
	the_mmi.brainmob.set_suicide(suiciding)
	the_mmi.brain.suicided = suiciding
	if(the_core)
		var/obj/structure/ai_core/core = the_core
		core.brain = the_mmi
		the_mmi.forceMove(the_core)
	else
		the_mmi.forceMove(get_turf(src))
	if(the_mmi.brainmob.stat == DEAD && !suiciding)
		the_mmi.brainmob.set_stat(CONSCIOUS)
	if(mind)
		mind.transfer_to(the_mmi.brainmob)
	the_mmi.update_appearance()
	return TRUE

/mob/living/silicon/ai/Topic(href, href_list)
	..()
	if(usr != src && !istype(usr, /mob/living/silicon/robot/shell))
		return

	if(href_list["emergencyAPC"]) //This check comes before incapacitated() because the only time it would be useful is when we have no power.
		if(!apc_override)
			to_chat(src, span_notice("APC backdoor is no longer available."))
			return
		apc_override.ui_interact(src)
		return

	if(incapacitated())
		return

	if (href_list["mach_close"])
		var/t1 = "window=[href_list["mach_close"]]"
		unset_machine()
		src << browse(null, t1)
	if (href_list["switchcamera"])
		switchCamera(locate(href_list["switchcamera"]) in GLOB.cameranet.cameras)
	if (href_list["showalerts"])
		alert_control.ui_interact(src)
#ifdef AI_VOX
	if(href_list["say_word"])
		play_vox_word(href_list["say_word"], null, src)
		return
#endif
	if(href_list["show_paper"])
		if(last_paper_seen)
			src << browse(last_paper_seen, "window=show_paper")
	//Carn: holopad requests
	if(href_list["jumptoholopad"])
		var/obj/machinery/holopad/Holopad = locate(href_list["jumptoholopad"]) in GLOB.machines
		if(Holopad)
			cam_prev = get_turf(eyeobj)
			eyeobj.setLoc(Holopad)
		else
			to_chat(src, span_notice("Unable to locate the holopad."))
	if(href_list["track"])
		var/string = href_list["track"]
		trackable_mobs(src)
		var/list/trackeable = list()
		trackeable += track.humans + track.others
		var/list/target = list()
		for(var/I in trackeable)
			var/datum/weakref/to_resolve = trackeable[I]
			var/mob/to_track = to_resolve.resolve()
			if(!to_track || to_track.name != string)
				continue
			target += to_track
		if(name == string)
			target += src
		if(length(target))
			if(istype(usr, /mob/living/silicon/robot/shell))
				var/mob/living/silicon/robot/shell/S = usr
				S.ai_actual_track(pick(target))
			else
				ai_actual_track(pick(target))
		else
			to_chat(src, "Цель не видна на активных камерах.")
		return
	if (href_list["ai_take_control"]) //Mech domination
		var/obj/vehicle/sealed/mecha/M = locate(href_list["ai_take_control"]) in GLOB.mechas_list
		if (!M)
			return

		var/mech_has_controlbeacon = FALSE
		for(var/obj/item/mecha_parts/mecha_tracking/ai_control/A in M.trackers)
			mech_has_controlbeacon = TRUE
			break
		if(!can_dominate_mechs && !mech_has_controlbeacon)
			message_admins("Warning: possible href exploit by [key_name(usr)] - attempted control of a mecha without can_dominate_mechs or a control beacon in the mech.")
			log_game("Warning: possible href exploit by [key_name(usr)] - attempted control of a mecha without can_dominate_mechs or a control beacon in the mech.")
			return

		if(controlled_equipment)
			to_chat(src, span_warning("You are already loaded into an onboard computer!"))
			return
		if(!GLOB.cameranet.checkCameraVis(M))
			to_chat(src, span_warning("Exosuit is no longer near active cameras."))
			return
		if(!isturf(loc))
			to_chat(src, span_warning("You aren't in your core!"))
			return
		if(M)
			M.transfer_ai(AI_MECH_HACK, src, usr) //Called om the mech itself.


/mob/living/silicon/ai/proc/switchCamera(obj/machinery/camera/C)
	if(QDELETED(C))
		return FALSE

	if(!tracking)
		cameraFollow = null

	if(QDELETED(eyeobj))
		view_core()
		return
	// ok, we're alive, camera is good and in our network...
	eyeobj.setLoc(get_turf(C))
	return TRUE

/mob/living/silicon/ai/proc/botcall()
	set category = "AI Commands"
	set name = "Access Robot Control"
	set desc = "Wirelessly control various automatic robots."

	if(!robot_control)
		robot_control = new(src)

	robot_control.ui_interact(src)

/mob/living/silicon/ai/proc/set_waypoint(atom/A)
	var/turf/turf_check = get_turf(A)
		//The target must be in view of a camera or near the core.
	if(turf_check in range(get_turf(src)))
		call_bot(turf_check)
	else if(GLOB.cameranet && GLOB.cameranet.checkTurfVis(turf_check))
		call_bot(turf_check)
	else
		to_chat(src, span_danger("Selected location is not visible."))

/mob/living/silicon/ai/proc/call_bot(turf/waypoint)

	if(!Bot)
		return

	if(Bot.calling_ai && Bot.calling_ai != src) //Prevents an override if another AI is controlling this bot.
		to_chat(src, span_danger("Interface error. Unit is already in use."))
		return
	to_chat(src, span_notice("Sending command to bot..."))
	call_bot_cooldown = world.time + CALL_BOT_COOLDOWN
	Bot.call_bot(src, waypoint)
	call_bot_cooldown = 0

/mob/living/silicon/ai/proc/alarm_triggered(datum/source, alarm_type, area/source_area)
	SIGNAL_HANDLER
	var/list/cameras = source_area.cameras
	var/home_name = source_area.name

	if (length(cameras))
		var/obj/machinery/camera/cam = cameras[1]
		if (cam.can_use())
			queueAlarm("--- [alarm_type] alarm detected in [home_name]! (<A HREF=?src=[REF(src)];switchcamera=[REF(cam)]>[cam.c_tag]</A>)", alarm_type)
			if(alarm_type == ALARM_MOTION)
				playsound(get_turf(src), 'sound/machines/twobeep.ogg', 40, TRUE)
		else
			var/first_run = FALSE
			var/dat2 = ""
			for (var/obj/machinery/camera/camera as anything in cameras)
				dat2 += "[(!first_run) ? "" : " | "]<A HREF=?src=[REF(src)];switchcamera=[REF(camera)]>[camera.c_tag]</A>"
				first_run = TRUE
			queueAlarm("--- [alarm_type] alarm detected in [home_name]! ([dat2])", alarm_type)
			if(alarm_type == ALARM_MOTION)
				playsound(get_turf(src), 'sound/machines/twobeep.ogg', 40, TRUE)
	else
		queueAlarm("--- [alarm_type] alarm detected in [home_name]! (No Camera)", alarm_type)
		if(alarm_type == ALARM_MOTION)
			playsound(get_turf(src), 'sound/machines/twobeep.ogg', 40, TRUE)
	return 1

/mob/living/silicon/ai/proc/alarm_cleared(datum/source, alarm_type, area/source_area)
	SIGNAL_HANDLER
	queueAlarm("--- [alarm_type] alarm in [source_area.name] has been cleared.", alarm_type, 0)

//Replaces /mob/living/silicon/ai/verb/change_network() in ai.dm & camera.dm
//Adds in /mob/living/silicon/ai/proc/ai_network_change() instead
//Addition by Mord_Sith to define AI's network change ability
/mob/living/silicon/ai/proc/ai_network_change()
	set category = "AI Commands"
	set name = "Jump To Network"
	unset_machine()
	cameraFollow = null
	var/cameralist[0]

	if(incapacitated())
		return

	var/mob/living/silicon/ai/U = usr

	for (var/obj/machinery/camera/C in GLOB.cameranet.cameras)
		var/turf/camera_turf = get_turf(C) //get camera's turf in case it's built into something so we don't get z=0
		var/list/tempnetwork = C.network
		if(!camera_turf || !(is_station_level(camera_turf.z) || is_mining_level(camera_turf.z) || ("ss13" in tempnetwork)))
			continue
		if(!C.can_use())
			continue

		tempnetwork.Remove("rd", "toxins", "prison")
		if(tempnetwork.len)
			for(var/i in C.network)
				cameralist[i] = i
	var/old_network = network
	network = tgui_input_list(U, "Какую сеть желаете просмотреть?", "Камерные Сети", sort_list(cameralist))

	if(!U.eyeobj)
		U.view_core()
		return

	if(isnull(network))
		network = old_network // If nothing is selected
	else
		for(var/obj/machinery/camera/C in GLOB.cameranet.cameras)
			if(!C.can_use())
				continue
			if(network in C.network)
				U.eyeobj.setLoc(get_turf(C))
				break
	to_chat(src, span_notice("Переключаемся на сеть \"[uppertext(network)]\"."))
//End of code by Mord_Sith

/mob/living/silicon/ai/proc/ai_statuschange()
	set category = "AI Commands"
	set name = "AI Status"

	if(incapacitated())
		return
	var/list/ai_emotions = list("Very Happy", "Happy", "Neutral", "Unsure", "Confused", "Sad", "BSOD", "Blank", "Problems?", "Awesome", "Facepalm", "Thinking", "Friend Computer", "Dorfy", "Blue Glow", "Red Glow")
	var/emote = tgui_input_list(usr, "Пожалуйста, выберите статус!", "Статус ИИ", sort_list(ai_emotions))
	for (var/each in GLOB.ai_status_displays) //change status of displays
		var/obj/machinery/status_display/ai/M = each
		M.emotion = emote
		M.update()
	if (emote == "Friend Computer")
		var/datum/radio_frequency/frequency = SSradio.return_frequency(FREQ_STATUS_DISPLAYS)

		if(!frequency)
			return

		var/datum/signal/status_signal = new(list("command" = "friendcomputer"))
		frequency.post_signal(src, status_signal)
	return

//I am the icon meister. Bow fefore me.	//>fefore
/mob/living/silicon/ai/proc/ai_hologram_change()
	set name = "Change Hologram"
	set desc = "Change the default hologram available to AI to something else."
	set category = "AI Commands"

	if(incapacitated())
		return
	var/input
	switch(tgui_alert(usr,"Would you like to select a hologram based on a crew member, an animal, or switch to a unique avatar?",,list("Crew Member","Unique","Animal")))
		if("Crew Member")
			var/list/personnel_list = list()

			for(var/datum/data/record/t in GLOB.data_core.locked)//Look in data core locked.
				personnel_list["[t.fields["name"]]: [t.fields["rank"]]"] = t.fields["image"]//Pull names, rank, and image.

			if(personnel_list.len)
				input = tgui_input_list(usr, "Выберите члена экипажа", "Члены Экипажа", sort_list(personnel_list))
				var/icon/character_icon = personnel_list[input]
				if(character_icon)
					qdel(holo_icon)//Clear old icon so we're not storing it in memory.
					holo_icon = getHologramIcon(icon(character_icon))
			else
				tgui_alert(usr,"No suitable records found. Aborting.")

		if("Animal")
			var/list/icon_list = list(
			"bear" = 'icons/mob/animal.dmi',
			"carp" = 'icons/mob/carp.dmi',
			"chicken" = 'icons/mob/animal.dmi',
			"corgi" = 'icons/mob/pets.dmi',
			"cow" = 'icons/mob/animal.dmi',
			"crab" = 'icons/mob/animal.dmi',
			"fox" = 'icons/mob/pets.dmi',
			"goat" = 'icons/mob/animal.dmi',
			"cat" = 'icons/mob/pets.dmi',
			"cat2" = 'icons/mob/pets.dmi',
			"poly" = 'icons/mob/animal.dmi',
			"pug" = 'icons/mob/pets.dmi',
			"spider" = 'icons/mob/animal.dmi'
			)

			input = tgui_input_list(usr, "Выберите голограмму", "Голограммы", sort_list(icon_list))
			if(isnull(input))
				return
			if(isnull(icon_list[input]))
				return
			qdel(holo_icon)
			switch(input)
				if("poly")
					holo_icon = getHologramIcon(icon(icon_list[input],"parrot_fly"))
				if("chicken")
					holo_icon = getHologramIcon(icon(icon_list[input],"chicken_brown"))
				if("spider")
					holo_icon = getHologramIcon(icon(icon_list[input],"guard"))
				else
					holo_icon = getHologramIcon(icon(icon_list[input], input))
		else
			var/list/icon_list = list(
				"default" = 'icons/mob/ai.dmi',
				"floating face" = 'icons/mob/ai.dmi',
				"xeno queen" = 'icons/mob/alien.dmi',
				"horror" = 'icons/mob/ai.dmi',
				"clock" = 'icons/mob/ai.dmi'
				)

			input = tgui_input_list(usr, "Выберите голограмму", "Голограммы", sort_list(icon_list))
			if(isnull(input))
				return
			if(isnull(icon_list[input]))
				return
			qdel(holo_icon)
			switch(input)
				if("xeno queen")
					holo_icon = getHologramIcon(icon(icon_list[input],"alienq"))
				else
					holo_icon = getHologramIcon(icon(icon_list[input], input))
	return

/datum/action/innate/core_return
	name = "Return to Main Core"
	desc = "Leave the APC and resume normal core operations."
	button_icon = 'icons/mob/actions/actions_AI.dmi'
	button_icon_state = "ai_malf_core"

/datum/action/innate/core_return/Activate()
	var/obj/machinery/power/apc/apc = owner.loc
	if(!istype(apc))
		to_chat(owner, span_notice("You are already in your Main Core."))
		return
	apc.malfvacate()
	qdel(src)

/mob/living/silicon/ai/proc/toggle_camera_light()
	camera_light_on = !camera_light_on

	if (!camera_light_on)
		to_chat(src, "Camera lights deactivated.")

		for (var/obj/machinery/camera/C in lit_cameras)
			C.set_light(0)
			lit_cameras = list()

		return

	light_cameras()

	to_chat(src, "Camera lights activated.")

//AI_CAMERA_LUMINOSITY

/mob/living/silicon/ai/proc/light_cameras()
	var/list/obj/machinery/camera/add = list()
	var/list/obj/machinery/camera/remove = list()
	var/list/obj/machinery/camera/visible = list()
	for (var/datum/camerachunk/chunk as anything in eyeobj.visibleCameraChunks)
		for (var/z_key in chunk.cameras)
			for(var/obj/machinery/camera/camera as anything in chunk.cameras[z_key])
				if (!camera.can_use() || get_dist(camera, eyeobj) > 7 || !camera.internal_light)
					continue
				visible |= camera

	add = visible - lit_cameras
	remove = lit_cameras - visible

	for (var/obj/machinery/camera/C in remove)
		lit_cameras -= C //Removed from list before turning off the light so that it doesn't check the AI looking away.
		C.Togglelight(0)
	for (var/obj/machinery/camera/C in add)
		C.Togglelight(1)
		lit_cameras |= C

/mob/living/silicon/ai/proc/control_integrated_radio()
	set name = "Transceiver Settings"
	set desc = "Allows you to change settings of your radio."
	set category = "AI Commands"

	if(incapacitated())
		return

	to_chat(src, "Accessing Subspace Transceiver control...")
	if (radio)
		radio.interact(src)

/mob/living/silicon/ai/proc/set_syndie_radio()
	if(radio)
		radio.make_syndie()

/mob/living/silicon/ai/proc/set_automatic_say_channel()
	set name = "Set Auto Announce Mode"
	set desc = "Modify the default radio setting for your automatic announcements."
	set category = "AI Commands"

	if(incapacitated())
		return
	set_autosay()

/mob/living/silicon/ai/transfer_ai(interaction, mob/user, mob/living/silicon/ai/AI, obj/item/aicard/card)
	if(!..())
		return
	if(interaction == AI_TRANS_TO_CARD)//The only possible interaction. Upload AI mob to a card.
		if(!can_be_carded)
			to_chat(user, span_boldwarning("Transfer failed."))
			return
		disconnect_shell() //If the AI is controlling a borg, force the player back to core!
		if(!mind)
			to_chat(user, span_warning("No intelligence patterns detected."))
			return
		ShutOffDoomsdayDevice()
		var/obj/structure/ai_core/new_core = new /obj/structure/ai_core/deactivated(loc)//Spawns a deactivated terminal at AI location.
		new_core.circuit.battery = battery
		ai_restore_power()//So the AI initially has power.
		control_disabled = TRUE //Can't control things remotely if you're stuck in a card!
		radio_enabled = FALSE 	//No talking on the built-in radio for you either!
		forceMove(card)
		card.AI = src
		to_chat(src, "You have been downloaded to a mobile storage device. Remote device connection severed.")
		to_chat(user, "<span class='boldnotice'>Transfer successful</span>: [name] ([rand(1000,9999)].exe) removed from host terminal and stored within local memory.")

/mob/living/silicon/ai/canUseTopic(atom/movable/M, be_close=FALSE, no_dexterity=FALSE, no_tk=FALSE, need_hands = FALSE, floor_okay=FALSE)
	if(control_disabled)
		to_chat(src, span_warning("Не могу сделать это сейчас!"))
		return FALSE
	return can_see(M) && ..() //stop AIs from leaving windows open and using then after they lose vision

/mob/living/silicon/ai/proc/can_see(atom/A)
	if(isturf(loc)) //AI in core, check if on cameras
		//get_turf_pixel() is because APCs in maint aren't actually in view of the inner camera
		//apc_override is needed here because AIs use their own APC when depowered
		return ((GLOB.cameranet && GLOB.cameranet.checkTurfVis(get_turf_pixel(A))) || (A == apc_override))
	//AI is carded/shunted
	//view(src) returns nothing for carded/shunted AIs and they have X-ray vision so just use get_dist
	var/list/viewscale = getviewsize(client.view)
	return get_dist(src, A) <= max(viewscale[1]*0.5,viewscale[2]*0.5)

/mob/living/silicon/ai/proc/relay_speech(message, atom/movable/speaker, datum/language/message_language, raw_message, radio_freq, list/spans, list/message_mods = list())
	var/treated_message = lang_treat(speaker, message_language, raw_message, spans, message_mods)
	var/start = "Relayed Speech: "
	var/namepart = "[speaker.GetVoice()]"
	var/hrefpart = "<a href='?src=[REF(src)];track=[html_encode(namepart)]'>"
	var/jobpart = "Unknown"

	if (iscarbon(speaker))
		var/mob/living/carbon/S = speaker
		if(S.job)
			jobpart = "[S.job]"

	var/rendered = "<i><span class='game say'>[start]<span class='name'>[hrefpart][namepart] ([jobpart])</a> </span><span class='message'>[treated_message]</span></span></i>"

	if (client?.prefs.chat_on_map && (client.prefs.see_chat_non_mob || ismob(speaker)) || isdead(src))
		create_chat_message(speaker, message_language, raw_message, spans)
	show_message(rendered, 2)

/mob/living/silicon/ai/fully_replace_character_name(oldname,newname)
	..()
	if(oldname != real_name)
		if(eyeobj)
			eyeobj.name = "[newname] (око ИИ)"
			modularInterface?.saved_identification = real_name

		// Notify Cyborgs
		for(var/mob/living/silicon/robot/Slave in connected_robots)
			Slave.show_laws()

/datum/action/innate/choose_modules
	name = "Malfunction Modules"
	desc = "Choose from a variety of insidious modules to aid you."
	button_icon = 'icons/mob/actions/actions_AI.dmi'
	button_icon_state = "modules_menu"
	var/datum/module_picker/module_picker

/datum/action/innate/choose_modules/New(picker)
	. = ..()
	if(istype(picker, /datum/module_picker))
		module_picker = picker
	else
		CRASH("choose_modules action created with non module picker")

/datum/action/innate/choose_modules/Activate()
	module_picker.ui_interact(owner)

/mob/living/silicon/ai/proc/add_malf_picker()
	to_chat(src, "In the top left corner of the screen you will find the Malfunction Modules button, where you can purchase various abilities, from upgraded surveillance to station ending doomsday devices.")
	to_chat(src, "You are also capable of hacking APCs, which grants you more points to spend on your Malfunction powers. The drawback is that a hacked APC will give you away if spotted by the crew. Hacking an APC takes 60 seconds.")
	view_core() //A BYOND bug requires you to be viewing your core before your verbs update
	malf_picker = new /datum/module_picker
	modules_action = new(malf_picker)
	modules_action.Grant(src)

/mob/living/silicon/ai/reset_perspective(atom/new_eye)
	SHOULD_CALL_PARENT(FALSE) // I hate you all
	if(camera_light_on)
		light_cameras()
	if(istype(new_eye, /obj/machinery/camera))
		current = new_eye
	if(!client)
		return

	if(ismovable(new_eye))
		if(new_eye != GLOB.ai_camera_room_landmark)
			end_multicam()
		client.perspective = EYE_PERSPECTIVE
		client.set_eye(new_eye)
	else
		end_multicam()
		if(isturf(loc))
			if(eyeobj)
				client.set_eye(eyeobj)
				client.perspective = EYE_PERSPECTIVE
			else
				client.set_eye(client.mob)
				client.perspective = MOB_PERSPECTIVE
		else
			client.perspective = EYE_PERSPECTIVE
			client.set_eye(loc)
	update_sight()
	if(client.eye != src)
		var/atom/AT = client.eye
		AT.get_remote_view_fullscreens(src)
	else
		clear_fullscreen("remote_view", 0)

	// I am so sorry
	SEND_SIGNAL(src, COMSIG_MOB_RESET_PERSPECTIVE)

/mob/living/silicon/ai/revive(full_heal = FALSE, admin_revive = FALSE)
	. = ..()
	if(.) //successfully ressuscitated from death
		set_core_display_icon(display_icon_override)
		set_eyeobj_visible(TRUE)

/mob/living/silicon/ai/proc/malfhacked(obj/machinery/power/apc/apc)
	malfhack = null
	malfhacking = 0
	clear_alert("hackingapc")

	if(!istype(apc) || QDELETED(apc) || apc.machine_stat & BROKEN)
		to_chat(src, span_danger("Hack aborted. The designated APC no longer exists on the power network."))
		playsound(get_turf(src), 'white/valtos/sounds/error2.ogg', 50, TRUE, ignore_walls = FALSE)
	else if(apc.aidisabled)
		to_chat(src, span_danger("Hack aborted. [apc] is no longer responding to our systems."))
		playsound(get_turf(src), 'white/valtos/sounds/error1.ogg', 50, TRUE, ignore_walls = FALSE)
	else
		malf_picker.processing_time += 10

		apc.malfai = parent || src
		apc.malfhack = TRUE
		apc.locked = TRUE
		apc.coverlocked = TRUE

		playsound(get_turf(src), 'sound/machines/ding.ogg', 50, TRUE, ignore_walls = FALSE)
		to_chat(src, "Hack complete. [apc] is now under your exclusive control.")
		apc.update_appearance()

/mob/living/silicon/ai/verb/deploy_to_shell(mob/living/silicon/robot/target)
	set category = "AI Commands"
	set name = "Deploy to Shell"

	if(incapacitated())
		return
	if(control_disabled)
		to_chat(src, span_warning("Wireless networking module is offline."))
		return

	var/list/possible = list()

	for(var/borgie in GLOB.available_ai_shells)
		var/mob/living/silicon/robot/R = borgie
		if(R.shell && !R.deployed && (R.stat != DEAD) && (!R.connected_ai ||(R.connected_ai == src)) || (R.ratvar && !is_servant_of_ratvar(src)))
			possible += R

	if(!LAZYLEN(possible))
		to_chat(src, "No usable AI shell beacons detected.")

	if(!target || !(target in possible)) //If the AI is looking for a new shell, or its pre-selected shell is no longer valid
		target = tgui_input_list(src, "Какую оболочку хотите контролировать?", "Прямое Управление", sort_names(possible))

	if (!target || target.stat == DEAD || target.deployed || !(!target.connected_ai ||(target.connected_ai == src)) || (target.ratvar && !is_servant_of_ratvar(src)))
		return

	else if(mind)
		RegisterSignal(target, COMSIG_LIVING_DEATH, PROC_REF(disconnect_shell))
		deployed_shell = target
		if(is_servant_of_ratvar(src) && !deployed_shell.ratvar)
			deployed_shell.SetRatvar(TRUE)
		target.deploy_init(src)
		mind.transfer_to(target)
	diag_hud_set_deployed()

/datum/action/innate/deploy_shell
	name = "Deploy to AI Shell"
	desc = "Wirelessly control a specialized cyborg shell."
	button_icon = 'icons/mob/actions/actions_AI.dmi'
	button_icon_state = "ai_shell"

/datum/action/innate/deploy_shell/Trigger(trigger_flags)
	var/mob/living/silicon/ai/AI = owner
	if(!AI)
		return
	AI.deploy_to_shell()

/datum/action/innate/deploy_last_shell
	name = "Reconnect to shell"
	desc = "Reconnect to the most recently used AI shell."
	button_icon = 'icons/mob/actions/actions_AI.dmi'
	button_icon_state = "ai_last_shell"
	var/mob/living/silicon/robot/last_used_shell

/datum/action/innate/deploy_last_shell/Trigger(trigger_flags)
	if(!owner)
		return
	if(last_used_shell)
		var/mob/living/silicon/ai/AI = owner
		AI.deploy_to_shell(last_used_shell)
	else
		Remove(owner) //If the last shell is blown, destroy it.

/mob/living/silicon/ai/proc/disconnect_shell()
	SIGNAL_HANDLER
	if(deployed_shell) //Forcibly call back AI in event of things such as damage, EMP or power loss.
		to_chat(src, span_danger("Your remote connection has been reset!"))
		deployed_shell.undeploy()
		UnregisterSignal(deployed_shell, COMSIG_LIVING_DEATH)
	diag_hud_set_deployed()

/mob/living/silicon/ai/resist()
	return

/mob/living/silicon/ai/spawned/Initialize(mapload, datum/ai_laws/L, mob/target_ai)
	if(!target_ai)
		target_ai = src //cheat! just give... ourselves as the spawned AI, because that's technically correct
	. = ..()

/mob/living/silicon/ai/proc/camera_visibility(mob/camera/ai_eye/moved_eye)
	GLOB.cameranet.visibility(moved_eye, client, all_eyes, TRUE)

/mob/living/silicon/ai/forceMove(atom/destination)
	. = ..()
	if(.)
		end_multicam()

/mob/living/silicon/ai/up()
	set name = "Выше"
	set category = "IC"

	if(eyeobj.zMove(UP, z_move_flags = ZMOVE_FEEDBACK))
		to_chat(src, span_notice("Поднимаюсь на уровень выше."))

/mob/living/silicon/ai/down()
	set name = "Ниже"
	set category = "IC"

	if(eyeobj.zMove(DOWN, z_move_flags = ZMOVE_FEEDBACK))
		to_chat(src, span_notice("Опускаюсь на уровень ниже."))


/// Proc to hook behavior to the changes of the value of [aiRestorePowerRoutine].
/mob/living/silicon/ai/proc/setAiRestorePowerRoutine(new_value)
	if(new_value == aiRestorePowerRoutine)
		return
	. = aiRestorePowerRoutine
	aiRestorePowerRoutine = new_value
	if(aiRestorePowerRoutine)
		if(!.)
			ADD_TRAIT(src, TRAIT_INCAPACITATED, POWER_LACK_TRAIT)
	else if(.)
		REMOVE_TRAIT(src, TRAIT_INCAPACITATED, POWER_LACK_TRAIT)


/mob/living/silicon/on_handsblocked_start()
	return // AIs have no hands

/mob/living/silicon/on_handsblocked_end()
	return // AIs have no hands
